# Maintainer: justforlxz <justforlxz@gmail.com>

pkgname=golang-deepin-gir
pkgver=3.0.0
pkgrel=1
pkgdesc='Generate static golang bindings for GObject'
arch=('any')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gtk3' 'libgudev')
makedepends=('git' 'go' 'gobject-introspection')
checkdepends=('golang-gopkg-check.v1')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-gir-generator/archive/$pkgver.tar.gz")
sha512sums=('SKIP')

prepare() {
  cd $(echo $url | awk -F/ '{print $NF}')-$pkgver
  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin/

  # Should be fixed upstream
  mkdir -p out/src/github.com/linuxdeepin/go-gir/{glib-2.0,gobject-2.0,gio-2.0,gudev-1.0}

  # https://github.com/linuxdeepin/developer-center/issues/955
  sed -i "s/'Can'tFind'/“Can'tFind”/" lib.in/glib-2.0/keyfile_test.go
}

build() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GO111MODULE=off
  cd $(echo $url | awk -F/ '{print $NF}')-$pkgver
  rm -r gogtk-demo
  make

  mv out/src/github.com/linuxdeepin/go-gir "$srcdir"/build/src/github.com/linuxdeepin/
}

check() {
  # https://github.com/linuxdeepin/developer-center/issues/955
  export GOPATH="$srcdir/build:/usr/share/gocode"
  cd "$srcdir"/build/src/github.com/linuxdeepin/go-gir
  go test -v $(go list ./...)

  cd "$srcdir/$pkgname"
  go run test/memory.go
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin
  cp -a "$srcdir"/build/src/github.com/linuxdeepin/go-gir "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin
}
