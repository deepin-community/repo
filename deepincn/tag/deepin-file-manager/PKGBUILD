# Maintainer: justforlxz <justforlxz@gmail.com>

pkgname=deepin-file-manager
pkgver=5.5.1
pkgrel=1
pkgdesc='Deepin File Manager'
arch=('x86_64' 'aarch64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-anything' 'disomaster' 'file' 'gio-qt' 'libmediainfo' 'avfs' 'polkit-qt5' 'poppler'
         'ffmpegthumbnailer' 'startdde' 'taglib' 'jemalloc' 'htmlcxx' 'mimetic' 'lucene++')
makedepends=('git' 'deepin-movie' 'lucene++' 'jemalloc' 'kcodecs' 'htmlcxx' 'libgsf' 'mimetic' 'boost' 'boost-libs' 'qt5-tools' 'deepin-dock' 'deepin-gettext-tools' 'gtest')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('SKIP')

prepare() {
    cd ${pkgname}
    sed -i '/#include <QException>/a #include <QPainterPath>' src/dialogs/dfmtaskwidget.cpp
    sed -i '/#include <QTimer>/a #include <QPainterPath>' src/dde-file-manager-lib/interfaces/dfmglobal.cpp
    sed -i '/#include <QPainter>/a #include <QPainterPath>' src/dde-file-manager-lib/interfaces/{dlistitemdelegate,dfmstyleditemdelegate,diconitemdelegate}.cpp src/dde-file-manager-lib/dialogs/openwithdialog.cpp
    sed -i 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' src/dde-file-manager-daemon/dde-file-manager-daemon.pro
}

build() {
  cd ${pkgname}
  qmake-qt5 PREFIX=/usr filemanager.pro
  make
}

package() {
  cd ${pkgname}
  make INSTALL_ROOT="$pkgdir" install
}
